!---------------------------------------------------------------------------------------------------------------------------------
!  Using the historical format for the VARIANT NHFLUX file, import/export data from the binary file
!  Note that the adjoint fluxes are handled by the calling routine and that the vectors are automatically reordered NGROUP,NGROUP-1,NGROUP-2,...
!---------------------------------------------------------------------------------------------------------------------------------
!  NHFLUX_ASSIGNPRINTINFO     !  Sets the output unit and debugprint level settings for the module
!  NHFLUX_FindActiveNode      !  Finds the NHFLUX node ordering for the GEODST node
!  NHFLUX_PRINT               !  Prints the NHFLUX data structure
!  NHFLUX_DEFINE              !  Allocates and initializes the NHFLUX data structure
!  NHFLUX_VOID                !  Deallocates the data
! ----------------------------
!  NHFLUX_ASSIGNPRINTINFO(OUTPUT_UNIT)
!  NHFLUX_FindActiveNode(USER_NHFLUX,GEODST_XYNode,NHFLUX_XYNode)
!  NHFLUX_PRINT(USER_NHFLUX)
!  NHFLUX_DEFINE(USER_NHFLUX,NSURF,NINTXY,NPCBDY,NPCSYM,NPCSEC,NMOM,NINTK,NSCOEF,NPCXY,NGROUP,NMOMS,IWNHFL)
!  NHFLUX_VOID(USER_NHFLUX)
!---------------------------------------------------------------------------------------------------------------------------------
MODULE NHFLUX_io
#include "DIF3D_Types.h"
IMPLICIT NONE

TYPE NHFLUX_DATA
   DIF3D_Log       :: DEFINED  = .FALSE.
   DIF3D_FileNames :: FILENAME = 'UNDEFINE'  ! Originating file name
   ! CARD TYPE 0
   DIF3D_Char        :: HNAME    = 'UNDEFINE'
   DIF3D_Char        :: HUSE(2)  = 'UNDEFINE'
   NHFLUX_Int  :: IVERS    = 0
   DIF3D_Log   :: ContainsForwardData = .TRUE.
   ! CARD TYPE 1
   NHFLUX_Int  :: NDIM              ! NUMBER OF DIMENSIONS.
   NHFLUX_Int  :: NGROUP            ! NUMBER OF ENERGY GROUPS.
   NHFLUX_Int  :: NINTI             ! NUMBER OF FIRST DIMENSION FINE MESH INTERVALS.
   NHFLUX_Int  :: NINTJ             ! NUMBER OF SECOND DIMENSION FINE MESH INTERVALS.
   NHFLUX_Int  :: NINTK             ! NUMBER OF THIRD DIMENSION FINE MESH INTERVALS. NINTK.EQ.1 IF NDIM.LE.2 
   NHFLUX_Int  :: ITER              ! OUTER ITERATION NUMBER AT WHICH FLUX WAS WRITTEN.
   NHFLUX_Real4:: EFFK             ! EFFECTIVE MULTIPLICATION FACTOR.
   NHFLUX_Real4:: POWER            ! POWER IN WATTS TO WHICH FLUX IS NORMALIZED.
   NHFLUX_Int  :: NSURF             ! NUMBER OF XY-PLANE SURFACES PER NODE.
   NHFLUX_Int  :: NMOM              ! NUMBER OF FLUX MOMENTS IN NODAL APPROXIMATION
   NHFLUX_Int  :: NINTXY            ! NUMBER OF MESH CELLS (NODES) ON XY-PLANE
   NHFLUX_Int  :: NPCXY             ! NUMBER OF XY-DIRECTED PARTIAL CURRENTS ON XY-PLANE.
   NHFLUX_Int  :: NSCOEF            ! NUMBER OF PARTIAL CURRENT MOMENTS PER NODE SURFACE
   NHFLUX_Int  :: ITRORD            ! ORDER OF THE POLYNOMIAL APPROXIMATION OF THE SOURCE WITHIN THE NODE
   NHFLUX_Int  :: IAPRX             ! ORDER OF THE POLYNOMIAL APPROXIMATION OF THE FLUXES WITHIN THE NODE
   NHFLUX_Int  :: ILEAK             ! ORDER OF THE POLYNOMIAL APPROXIMATION OF THE LEAKAGES ON THE SURFACES OF THE NODES
   NHFLUX_Int  :: IAPRXZ            ! ORDER OF THE PN EXPANSION OF THE FLUX
   NHFLUX_Int  :: ILEAKZ            ! ORDER OF THE PN EXPANSION OF THE LEAKAGE
   NHFLUX_Int  :: IORDER            ! DIF3D VERSION
                                    !   =0, VERSION 8.0 OR PREVIOUS ONES
                                    !   =2, VERSION 9.0
   NHFLUX_Int  :: NPCBDY            ! NUMBER OF OUTGOING PARTIAL CURRENTS ON OUTER XY-PLANE BOUNDARY.
   NHFLUX_Int  :: NPCSYM            ! NUMBER OF OUTGOING PARTIAL CURRENTS ON SYMMETRIC XY-PLANE BOUNDARY. HEXAGONAL GEOMETRY ONLY.
   NHFLUX_Int  :: NPCSEC            ! NUMBER OF OUTGOING PARTIAL CURRENTS ON SECTOR XY-PLANE BOUNDARY. HEXAGONAL GEOMETRY ONLY.
   NHFLUX_Int  :: IWNHFL  = 0       ! NHFLUX CONTENT
                                    !   =0, BOTH FLUXES AND PARTIAL CUURENTS ARE PRESENT IN THE FILE
                                    !   =1, ONLY FLUXES ARE PRESENT
                                    !   =2, ONLY PARTIAL CURRENTS ARE PRESENT
   NHFLUX_Int :: NMOMS              ! NUMBER OF ODD PARITY FLUX MOMENTS IN NODAL APPROX
   NHFLUX_Int :: DUM1               ! RESERVED FOR FUTURE USE
   NHFLUX_Int :: DUM2               ! RESERVED FOR FUTURE USE
   NHFLUX_Int :: DUM3               ! RESERVED FOR FUTURE USE
   NHFLUX_Int :: DUM4               ! RESERVED FOR FUTURE USE
   NHFLUX_Int :: DUM5               ! RESERVED FOR FUTURE USE
   NHFLUX_Int :: DUM6               ! RESERVED FOR FUTURE USE
   ! CARD TYPE 2
   NHFLUX_Int,DIMENSION(:,:),       POINTER :: IPCPNT  ! NSURF,NINTXY  IPCPNT(I,J)  POINTERS TO INCOMING XY-PLANE PARTIAL CURRENTS.
   NHFLUX_Int,DIMENSION(:),         POINTER :: IPCBDY  ! NPCBDY  IPCBDY(I)    POINTERS TO OUTGOING PARTIAL CURRENTS ON OUTER XY-PLANE BOUNDARY.
   NHFLUX_Int,DIMENSION(:),         POINTER :: ITRMAP  ! NINTXY  ITRMAP(I)    TRANSFORMATION MAP BETWEEN NODAL AND GEODST MESH CELL ORDERINGS.
   NHFLUX_Int,DIMENSION(:),         POINTER :: IPCSYM  ! NPCSYM+NPCSEC  IPCSYM(I)    POINTERS TO OUTGOING PARTIAL CURRENTS ON
   NHFLUX_Int,DIMENSION(:),         POINTER :: IPCSCP  ! NPCSYM+NPCSEC, IPCSTO(I)    POINTERS TO INGOING PARTIAL CURRENTS ON
   ! CARD TYPE 3
   NHFLUX_Real,DIMENSION(:,:,:,:),  POINTER :: FLUX    ! NMOM+NMOMS,NINTXY,NINTK,NGROUP   REGULAR FLUX MOMENTS BY NODE FOR THE PRESENT GROUP (This is the correct result)
   ! CARD TYPE 4
   NHFLUX_Real,DIMENSION(:,:,:,:),  POINTER :: PCURRH  ! NSCOEF,NPCXY,NINTK,NGROUP        OUTGOING XY-DIRECTED PARTIAL CURRENTS 
   ! CARD TYPE 5
   NHFLUX_Real,DIMENSION(:,:,:,:,:),POINTER :: PCURRZ  ! NSCOEF,NINTXY,2,NINTK+1,NGROUP  PCURRZ(I,J,K,L)  Z-DIRECTED PARTIAL CURRENTS IN
                                                       ! PLUS- (J=1) AND MINUS- (J=2) Z DIRECTIONS
                                                       ! ACROSS ALL AXIAL BOUNDARIES FOR THE PRESENT GROUP
                                                       ! E.G. INCOMING PARTIAL CURRENTS FOR NODE I ON AXIAL MESH INTERVAL K ARE PCURRZ(1,I,1) ON THE
                                                       ! LOWER BOUNDARY (RECORD K) AND PCURRZ(1,I,2) ON THE UPPER AXIAL BOUNDARY (RECORD K+1).
END TYPE

TYPE (NHFLUX_DATA), SAVE :: MODULE_NHFLUX ! A optional use "common block" data type for the module
DIF3D_Int :: MODULE_OUT = 6      ! Module output unit
PRIVATE  MODULE_OUT              ! Variables

CONTAINS

!---------------------------------------------------------------------------------------------------------------------------------
!  NHFLUX_ASSIGNPRINTINFO(OUTPUT_UNIT)
!  Sets the output unit and debugprint level settings for the module
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE NHFLUX_ASSIGNPRINTINFO(MODULE_OUTPUT_UNIT)
DIF3D_Int MODULE_OUTPUT_UNIT    ! Reset the object output unit

MODULE_OUT = MODULE_OUTPUT_UNIT

END SUBROUTINE NHFLUX_ASSIGNPRINTINFO

!---------------------------------------------------------------------------------------------------------------------------------
!  NHFLUX_FindActiveNode(USER_NHFLUX,GEODST_XYNode,NHFLUX_XYNode)
!  Finds the NHFLUX node ordering for the GEODST node
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE NHFLUX_FindActiveNode(USER_NHFLUX,GEODST_XYNode,NHFLUX_XYNode)
IMPLICIT NONE
! Passed in
TYPE (NHFLUX_DATA) USER_NHFLUX
DIF3D_Int GEODST_XYNode,NHFLUX_XYNode
! Local
DIF3D_Int I

100 FORMAT('[NHFLUX]...SORRY, BUT I MUST STOP')
101 FORMAT('[NHFLUX]',107('.'))
102 FORMAT('')
105 FORMAT('[NHFLUX]...THERE WAS A NON-FATAL ERROR THAT OCCURED IN (PRINT)',53('.'))

NHFLUX_XYNode = 0

IF (USER_NHFLUX%DEFINED) THEN  ! Something to do
   ! Identify which node we are in
   IF (GEODST_XYNode .NE. 0) THEN
      DO NHFLUX_XYNode = 1,USER_NHFLUX%NINTXY   ! The active nodes in the X-Y plane
         IF (USER_NHFLUX%ITRMAP(NHFLUX_XYNode) .EQ. GEODST_XYNode) EXIT ! We found an active node with this GEODST node assignment
      END DO
      IF (NHFLUX_XYNode .GT. USER_NHFLUX%NINTXY) NHFLUX_XYNode = 0 ! Didn't find it
   END IF
END IF

END SUBROUTINE NHFLUX_FindActiveNode

!---------------------------------------------------------------------------------------------------------------------------------
!  NHFLUX_PRINT(USER_NHFLUX)
!  Prints the NHFLUX data structure
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE NHFLUX_PRINT(USER_NHFLUX)
IMPLICIT NONE
! Passed in
TYPE (NHFLUX_DATA) USER_NHFLUX  
! Local
DIF3D_Int IOS,I,J,K,L,M,N

100 FORMAT('[NHFLUX]...SORRY, BUT I MUST STOP')
101 FORMAT('[NHFLUX]',107('.'))
102 FORMAT('')
105 FORMAT('[NHFLUX]...THERE WAS A NON-FATAL ERROR THAT OCCURED IN (PRINT)',53('.'))

IF (.NOT. USER_NHFLUX%DEFINED) THEN  ! Data to write doesn't exist
   WRITE(MODULE_OUT,101)
   WRITE(MODULE_OUT,'("[NHFLUX]...USER NHFLUX VARIABLE NOT DEFINED FOR PRINTING...NONFATAL",41("."))')
   WRITE(MODULE_OUT,101)
ELSE
   WRITE(MODULE_OUT,'("[NHFLUX]...HNAME.................................................",48("."),A16)') USER_NHFLUX%HNAME
   WRITE(MODULE_OUT,'("[NHFLUX]...HUSE(1)...............................................",48("."),A16)') USER_NHFLUX%HUSE(1)
   WRITE(MODULE_OUT,'("[NHFLUX]...HUSE(2)...............................................",48("."),A16)') USER_NHFLUX%HUSE(2)
   WRITE(MODULE_OUT,'("[NHFLUX]...IVERS.................................................",48("."),I16)') USER_NHFLUX%IVERS
   IF (USER_NHFLUX%ContainsForwardData) THEN
      WRITE(MODULE_OUT,'("[NHFLUX]...This data structure contains the forward flux solution",55("."))')
   ELSE
      WRITE(MODULE_OUT,'("[NHFLUX]...This data structure contains the adjoint flux solution",55("."))')
   END IF
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF DIMENSIONS..................................",48("."),I16)') USER_NHFLUX%NDIM
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF ENERGY GROUPS...............................",48("."),I16)') USER_NHFLUX%NGROUP
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF 1st DIM FINE MESH INTERVALS.................",48("."),I16)') USER_NHFLUX%NINTI
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF 2nd DIM FINE MESH INTERVALS.................",48("."),I16)') USER_NHFLUX%NINTJ
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF 3rd DIM FINE MESH INTERVALS.................",48("."),I16)') USER_NHFLUX%NINTK
   WRITE(MODULE_OUT,'("[NHFLUX]...OUTER ITER NUMBER AT WHICH FLUX WAS WRITTEN...........",48("."),I16)') USER_NHFLUX%ITER
   WRITE(MODULE_OUT,'("[NHFLUX]...EFFECTIVE MULTIPLICATION FACTOR.......................",48("."),E16.9)') USER_NHFLUX%EFFK
   WRITE(MODULE_OUT,'("[NHFLUX]...POWER IN WATTS TO WHICH FLUX IS NORMALIZED............",48("."),E16.9)') USER_NHFLUX%POWER
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF XY-PLANE SURFACES PER NODE..................",48("."),I16)') USER_NHFLUX%NSURF
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF FLUX MOMENTS IN NODAL APPROXIMATION.........",48("."),I16)') USER_NHFLUX%NMOM
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF MESH CELLS (NODES) ON XY-PLANE..............",48("."),I16)') USER_NHFLUX%NINTXY
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF XY-DIRECTED PARTIAL CURRENTS ON XY-PLANE....",48("."),I16)') USER_NHFLUX%NPCXY
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF PARTIAL CURRENT MOMENTS PER NODE SURFACE....",48("."),I16)') USER_NHFLUX%NSCOEF
   WRITE(MODULE_OUT,'("[NHFLUX]...ORDER OF THE POLYNOMIAL APPROXIMATION OF THE SOURCE...",48("."),I16)') USER_NHFLUX%ITRORD
   WRITE(MODULE_OUT,'("[NHFLUX]...ORDER OF THE POLYNOMIAL APPROXIMATION OF THE FLUXES...",48("."),I16)') USER_NHFLUX%IAPRX
   WRITE(MODULE_OUT,'("[NHFLUX]...ORDER OF THE POLYNOMIAL APPROXIMATION OF THE LEAKAGES.",48("."),I16)') USER_NHFLUX%ILEAK
   WRITE(MODULE_OUT,'("[NHFLUX]...ORDER OF THE PN EXPANSION OF THE FLUX.................",48("."),I16)') USER_NHFLUX%IAPRXZ
   WRITE(MODULE_OUT,'("[NHFLUX]...ORDER OF THE PN EXPANSION OF THE LEAKAGE..............",48("."),I16)') USER_NHFLUX%ILEAKZ
   WRITE(MODULE_OUT,'("[NHFLUX]...IORDER - DIF3D VERSION................................",48("."),I16)') USER_NHFLUX%IORDER
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON OUTER XY-PLANE BOUN.....",48("."),I16)') USER_NHFLUX%NPCBDY
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON SYMMETRIC XY-PLANE BOUN.",48("."),I16)') USER_NHFLUX%NPCSYM
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON SECTOR XY-PLANE BOUN....",48("."),I16)') USER_NHFLUX%NPCSEC
   WRITE(MODULE_OUT,'("[NHFLUX]...NHFLUX CONTENT........................................",48("."),I16)') USER_NHFLUX%IWNHFL
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF ODD PARITY FLUX MOMENTS IN NODAL APPROX.....",48("."),I16)') USER_NHFLUX%NMOMS
   WRITE(MODULE_OUT,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM1
   WRITE(MODULE_OUT,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM2
   WRITE(MODULE_OUT,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM3
   WRITE(MODULE_OUT,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM4
   WRITE(MODULE_OUT,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM5
   WRITE(MODULE_OUT,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM6

   DO J = 1,USER_NHFLUX%NINTXY
      WRITE(MODULE_OUT,'(("[NHFLUX]...POINTERS TO INCOMING XY PLANE PARTIAL CURRENTS...",8("[",I4,",",I16,"]",1X) ))') &
      (I,USER_NHFLUX%IPCPNT(I,J),I=1,USER_NHFLUX%NSURF)
   END DO
IF (USER_NHFLUX%NSURF .GT. 1) THEN
   DO I = 1,USER_NHFLUX%NPCBDY
      WRITE(MODULE_OUT,'("[NHFLUX]...POINTERS TO OUTGOING PARTIAL CURRENTS.................",I4,54("."),I16)') &
      I,USER_NHFLUX%IPCBDY(I)
   END DO
END IF
   DO I = 1,USER_NHFLUX%NINTXY
      WRITE(MODULE_OUT,'("[NHFLUX]...TRANSFORMATION MAP BETWEEN NODAL AND GEODST...........",I4,54("."),I16)') &
         I,USER_NHFLUX%ITRMAP(I)
   END DO
IF (USER_NHFLUX%NSURF .GT. 1) THEN
   DO I = 1,USER_NHFLUX%NPCSYM + USER_NHFLUX%NPCSEC
      WRITE(MODULE_OUT,'("[NHFLUX]...POINTERS TO OUTGOING PAR CURR ON SYM BOUND............",I4,54("."),I16)') &
         I,USER_NHFLUX%IPCSYM(I)
   END DO
   DO I = 1,USER_NHFLUX%NPCSYM + USER_NHFLUX%NPCSEC
      WRITE(MODULE_OUT,'("[NHFLUX]...POINTERS TO INGOING PAR CURR ON SYM AND SEC BOUND.....",I4,54("."),I16)') &
         I,USER_NHFLUX%IPCSCP(I)
   END DO
END IF

   DO N=1,USER_NHFLUX%NGROUP

   IF ( (USER_NHFLUX%IWNHFL .EQ. 0) .OR. (USER_NHFLUX%IWNHFL .EQ. 1)) THEN
      DO K = 1,USER_NHFLUX%NINTK
         WRITE(MODULE_OUT,'("[NHFLUX]...REGULAR FLUX MOMENTS FOR AXIAL PLANE.",I6)') K
         WRITE(MODULE_OUT,'("[NHFLUX]...XY Node; Nodal moments -> ")')
         DO J = 1,USER_NHFLUX%NINTXY
            WRITE(MODULE_OUT,'(I5,1X,5000(1PE16.9,1X))') J,(USER_NHFLUX%FLUX(I,J,K,N),I=1,USER_NHFLUX%NMOM+USER_NHFLUX%NMOMS)
         END DO
      END DO
   ENDIF

   IF ( (USER_NHFLUX%IWNHFL .EQ. 0) .OR. (USER_NHFLUX%IWNHFL .EQ. 2)) THEN
      DO K = 1,USER_NHFLUX%NINTK
         WRITE(MODULE_OUT,'("[NHFLUX]...RADIALLY DIRECTED PARTIAL CURRENTS FOR AXIAL PLANE.",I6)') K
         WRITE(MODULE_OUT,'("[NHFLUX]...XY Surface Index; Current moments -> ")')
         DO J = 1,USER_NHFLUX%NPCXY
            WRITE(MODULE_OUT,'(I5,1X,5000(1PE16.9,1X))') J,(USER_NHFLUX%PCURRH(I,J,K,N),I=1,USER_NHFLUX%NSCOEF)
         END DO
      END DO
   ENDIF

   IF ( (USER_NHFLUX%NDIM .EQ. 3) .AND. ((USER_NHFLUX%IWNHFL .EQ. 0) .OR. (USER_NHFLUX%IWNHFL .EQ. 2))) THEN
      DO J = 1,USER_NHFLUX%NINTK+1
         DO M = 1,2
            IF (M .EQ. 1) THEN
               WRITE(MODULE_OUT,'("[NHFLUX]...Z-DIRECTED PARTIAL CURRENTS FOR LOWER SURFACE AXIAL PLANE ",I6)') J
            ELSE
               WRITE(MODULE_OUT,'("[NHFLUX]...Z-DIRECTED PARTIAL CURRENTS FOR UPPER SURFACE AXIAL PLANE.",I6)') J
            END IF
            DO L=1,USER_NHFLUX%NINTXY
               WRITE(MODULE_OUT,'(I5,1X,5000(1PE16.9,1X))') L,(USER_NHFLUX%PCURRZ(K,L,M,J,N),K=1,USER_NHFLUX%NSCOEF)
            ENDDO
         END DO
      END DO
   ENDIF
   ENDDO ! Group
ENDIF

END SUBROUTINE NHFLUX_PRINT

!---------------------------------------------------------------------------------------------------------------------------------
!  NHFLUX_DEFINE(USER_NHFLUX,NSURF,NINTXY,NPCBDY,NPCSYM,NPCSEC,NMOM,NINTK,NSCOEF,NPCXY,NGROUP,NMOMS,IWNHFL)
!  Allocates and initializes the NHFLUX data structure
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE NHFLUX_DEFINE(USER_NHFLUX,NSURF,NINTXY,NPCBDY,NPCSYM,NPCSEC,NMOM,NINTK,NSCOEF,NPCXY,NGROUP,NMOMS,IWNHFL)
IMPLICIT NONE
!Passed in
TYPE(NHFLUX_DATA) USER_NHFLUX
DIF3D_Int :: NSURF
DIF3D_Int :: NINTXY
DIF3D_Int :: NPCBDY
DIF3D_Int :: NPCSYM
DIF3D_Int :: NPCSEC
DIF3D_Int :: NMOM
DIF3D_Int :: NINTK 
DIF3D_Int :: NSCOEF 
DIF3D_Int :: NPCXY
DIF3D_Int :: NGROUP
DIF3D_Int :: NMOMS
DIF3D_Int :: IWNHFL
! Local variables
DIF3D_Int IOS
DIF3D_Int NPCSTO

NPCSTO = NPCSYM+NPCSEC

100 FORMAT('[NHFLUX]...SORRY, BUT I MUST STOP')
101 FORMAT('[NHFLUX]',107('.'))
102 FORMAT('')
105 FORMAT('[NHFLUX]...THERE WAS A FATAL ERROR THAT OCCURED IN (ALLOCATE)',54('.'))

#ifdef DIF3D_Debug
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF XY-PLANE SURFACES PER NODE..................",48("."),I16)') NSURF
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF MESH CELLS (NODES) ON XY-PLANE..............",48("."),I16)') NINTXY
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON OUTER XY-PLANE BOUN.....",48("."),I16)') NPCBDY
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON SYMMETRIC XY-PLANE BOUN.",48("."),I16)') NPCSYM
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON SECTOR XY-PLANE BOUN....",48("."),I16)') NPCSEC
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF FLUX MOMENTS IN NODAL APPROXIMATION.........",48("."),I16)') NMOM
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF 3rd DIM FINE MESH INTERVALS.................",48("."),I16)') NINTK
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF PARTIAL CURRENT MOMENTS PER NODE SURFACE....",48("."),I16)') NSCOEF
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF XY-DIRECTED PARTIAL CURRENTS ON XY-PLANE....",48("."),I16)') NPCXY
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF ENERGY GROUPS...............................",48("."),I16)') NGROUP
   WRITE(MODULE_OUT,'("[NHFLUX]...NUMBER OF ODD PARITY FLUX MOMENTS IN NODAL APPROX.....",48("."),I16)') NMOMS
   WRITE(MODULE_OUT,'("[NHFLUX]...IWNHFL setting........................................",48("."),I16)') IWNHFL
#endif

IF (NSURF .LT. 1) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF XY-PLANE SURFACES PER NODE....................",40("."),I16)') NSURF
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF (NINTXY .LE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF MESH CELLS (NODES) ON XY-PLANE................",40("."),I16)') NINTXY
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF (NMOM .LE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF FLUX MOMENTS IN NODAL APPROXIMATION...........",40("."),I16)') NMOM
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF (NINTK .LE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF 3rd DIM FINE MESH INTERVALS...................",40("."),I16)') NINTK
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF (NGROUP .LE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF ENERGY GROUPS.................................",40("."),I16)') NGROUP
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF (NPCBDY .LT. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF OUTGOING PAR CUR ON OUTER XY-PLANE BOUN.......",40("."),I16)') NPCBDY
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF
IF (NSCOEF .LE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF PARTIAL CURRENT MOMENTS PER NODE SURFACE......",40("."),I16)') NSCOEF
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF
IF (NPCXY .LE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NUMBER OF XY-DIRECTED PARTIAL CURRENTS ON XY-PLANE......",40("."),I16)') NPCXY
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF
IF (NPCSTO .LT. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID NPCSYM+NPCSEC...........................................",40("."),I16)') NPCSTO
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF ((IWNHFL .LT. 0) .OR. (IWNHFL .GT. 2)) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...INVALID IWNHFL...........................................",40("."),I16)') IWNHFL
   WRITE(MODULE_OUT,100)
   CALL Abort
END IF

IF(NPCSTO .EQ. 0) NPCSTO = 1

IF (IWNHFL .EQ. 0) THEN
   ALLOCATE(USER_NHFLUX%IPCPNT(NSURF,NINTXY),USER_NHFLUX%IPCBDY(NPCBDY),USER_NHFLUX%ITRMAP(NINTXY),    &
         USER_NHFLUX%IPCSYM(NPCSTO),USER_NHFLUX%IPCSCP(NPCSTO),USER_NHFLUX%FLUX(NMOM+NMOMS,NINTXY,NINTK,NGROUP), &
         USER_NHFLUX%PCURRH(NSCOEF,NPCXY,NINTK,NGROUP),USER_NHFLUX%PCURRZ(NSCOEF,NINTXY,2,NINTK+1,NGROUP),STAT = IOS)
ELSE IF (IWNHFL .EQ. 1) THEN
   ALLOCATE(USER_NHFLUX%IPCPNT(NSURF,NINTXY),USER_NHFLUX%IPCBDY(NPCBDY),USER_NHFLUX%ITRMAP(NINTXY),    &
         USER_NHFLUX%IPCSYM(NPCSTO),USER_NHFLUX%IPCSCP(NPCSTO),USER_NHFLUX%FLUX(NMOM+NMOMS,NINTXY,NINTK,NGROUP), &
         USER_NHFLUX%PCURRH(1,1,1,1),USER_NHFLUX%PCURRZ(1,1,1,1,1),STAT = IOS)
ELSE 
   ALLOCATE(USER_NHFLUX%IPCPNT(NSURF,NINTXY),               &
            USER_NHFLUX%IPCBDY(NPCBDY),                     &
            USER_NHFLUX%ITRMAP(NINTXY),                     &
            USER_NHFLUX%IPCSYM(NPCSTO),                     &
            USER_NHFLUX%IPCSCP(NPCSTO),                     &
            USER_NHFLUX%FLUX(1,1,1,1),                      &
            USER_NHFLUX%PCURRH(NSCOEF,NPCXY,NINTK,NGROUP),  &
            USER_NHFLUX%PCURRZ(NSCOEF,NINTXY,2,NINTK+1,NGROUP),STAT = IOS)
END IF

IF (IOS .NE. 0) THEN
   WRITE(MODULE_OUT,105)
   WRITE(MODULE_OUT,'("[NHFLUX]...FAILED TO ALLOCATE NHFLUX DATA",58("."),I16)') IOS
   CALL Abort
END IF

USER_NHFLUX%DEFINED  = .TRUE.
USER_NHFLUX%NSURF    = NSURF
USER_NHFLUX%NINTXY   = NINTXY
USER_NHFLUX%NPCBDY   = NPCBDY
USER_NHFLUX%NPCSYM   = NPCSYM
USER_NHFLUX%NPCSEC   = NPCSEC
USER_NHFLUX%NMOM     = NMOM
USER_NHFLUX%NINTK    = NINTK 
USER_NHFLUX%NSCOEF   = NSCOEF
USER_NHFLUX%NPCXY    = NPCXY
USER_NHFLUX%NGROUP   = NGROUP
USER_NHFLUX%NMOMS    = NMOMS
USER_NHFLUX%IWNHFL   = IWNHFL

END SUBROUTINE NHFLUX_DEFINE

!---------------------------------------------------------------------------------------------------------------------------------
!  NHFLUX_VOID(USER_NHFLUX)
!  Deallocates the data
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE NHFLUX_VOID(USER_NHFLUX)
IMPLICIT NONE
TYPE (NHFLUX_DATA) USER_NHFLUX  ! A user data variable to be defined by reading in a NHFLUX file
! local
DIF3D_Int IOS

100 FORMAT('[NHFLUX]...SORRY, BUT I MUST STOP')
101 FORMAT('[NHFLUX]',107('.'))
102 FORMAT('')
105 FORMAT('[NHFLUX]...THERE WAS A FATAL ERROR THAT OCCURED IN (VOIDTYPE)',54('.'))

IF (USER_NHFLUX%DEFINED) THEN
   DEALLOCATE(USER_NHFLUX%IPCPNT,USER_NHFLUX%IPCBDY,USER_NHFLUX%ITRMAP, &
              USER_NHFLUX%IPCSYM,USER_NHFLUX%IPCSCP,USER_NHFLUX%FLUX,   &
              USER_NHFLUX%PCURRH,USER_NHFLUX%PCURRZ,STAT = IOS)
   IF (IOS .NE. 0) THEN
      WRITE(MODULE_OUT,105)
      WRITE(MODULE_OUT,'("[NHFLUX]...FAILED TO DEALLOCATE NHFLUX DATA",72("."))')
      WRITE(MODULE_OUT,100)
      CALL Abort
   END IF
   USER_NHFLUX%DEFINED = .FALSE.
END IF

END SUBROUTINE NHFLUX_VOID

END MODULE NHFLUX_io
