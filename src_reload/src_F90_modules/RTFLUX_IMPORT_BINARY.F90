!---------------------------------------------------------------------------------------------------------------------------------
!  RTFLUX_IMPORT_BINARY(USER_RTFLUX,USER_FILENAME)
!  Imports the binary RTFLUX data file
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE RTFLUX_IMPORT_BINARY(Output_Unit,USER_RTFLUX,USER_FILENAME)
USE RTFLUX_io
USE NE_Kind
#include "DIF3D_Types.h"
IMPLICIT NONE
! Passed in
DIF3D_Int Output_Unit
TYPE (RTFLUX_DATA) USER_RTFLUX  ! A user data variable to be defined by reading in a RTFLUX file
CHARACTER(*) USER_FILENAME
! Local
DIF3D_Int INPUTUNIT
DIF3D_Int IOS,I,J,K,G,M,JL,JU,BLOCKWIDTH

100 FORMAT('[RTFLUX]...SORRY, BUT I MUST STOP')
101 FORMAT('[RTFLUX]',107('.'))
102 FORMAT('')
105 FORMAT('[RTFLUX]...THERE WAS A FATAL ERROR THAT OCCURED IN (IMPORT_BINARY)',49('.'))

IF (USER_RTFLUX%DEFINED) CALL RTFLUX_VOID(USER_RTFLUX)   ! If the variable is already defined, void it

USER_RTFLUX%FILENAME = ADJUSTL(USER_FILENAME)
INPUTUNIT = NE_Kind_GetFreeLogicalUnit()

OPEN(UNIT=INPUTUNIT,IOSTAT=IOS,FILE=USER_FILENAME,STATUS='OLD',ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
IF (IOS .NE. 0) THEN
   WRITE(Output_Unit,105)
   WRITE(Output_Unit,'("[RTFLUX]...CANNOT OPEN FILE <",A80,">",5("."))') USER_FILENAME
   WRITE(Output_Unit,100)
   CALL Abort
END IF

! READ IN ISOTOPE NAMES (AND OTHER DATA) FROM RTFLUX FILE (TYPE 1-2)
#ifdef DIF3D_Debug
   WRITE(Output_Unit,101)
   WRITE(Output_Unit,'("[DLAYXS]...DEBUG PRINT OF RTFLUX IMPORT ROUTINE",52("."),A16)') USER_FILENAME
   WRITE(Output_Unit,101)
#endif
! Read Card Type 0
READ(INPUTUNIT) USER_RTFLUX%HNAME,(USER_RTFLUX%HUSE(I),I=1,2),USER_RTFLUX%IVERS
! Read Card Type 1

READ(INPUTUNIT) USER_RTFLUX%NDIM,USER_RTFLUX%NGROUP,USER_RTFLUX%NINTI,USER_RTFLUX%NINTJ,USER_RTFLUX%NINTK,  &
                USER_RTFLUX%ITER,USER_RTFLUX%EFFK,USER_RTFLUX%POWER,USER_RTFLUX%NBLOK

CALL RTFLUX_DEFINE(USER_RTFLUX,USER_RTFLUX%NINTI,USER_RTFLUX%NINTJ,USER_RTFLUX%NINTK,USER_RTFLUX%NGROUP)

! Read Card Type 2
IF (USER_RTFLUX%NDIM .EQ. 1) THEN
   BLOCKWIDTH = (USER_RTFLUX%NGROUP-1)/USER_RTFLUX%NBLOK+1 ! = (9-1)/2+1 = 5
   DO M = 1,USER_RTFLUX%NBLOK
      JL = (M-1)*BLOCKWIDTH+1      ! = 1;6
      JU = M*BLOCKWIDTH            ! = 5;9
      IF (JU .GT. USER_RTFLUX%NGROUP) JU = USER_RTFLUX%NGROUP
      READ(INPUTUNIT) ((USER_RTFLUX%FREG(I,1,1,J),I=1,USER_RTFLUX%NINTI),J=JL,JU)
   END DO
ELSE
   BLOCKWIDTH = (USER_RTFLUX%NINTJ-1)/USER_RTFLUX%NBLOK+1 ! = (9-1)/2+1 = 5
   DO G = 1,USER_RTFLUX%NGROUP
      DO K = 1,USER_RTFLUX%NINTK
         DO M = 1,USER_RTFLUX%NBLOK
            JL = (M-1)*BLOCKWIDTH+1      ! = 1;6
            JU = M*BLOCKWIDTH            ! = 5;9
            IF (JU .GT. USER_RTFLUX%NINTJ) JU = USER_RTFLUX%NINTJ
            READ(INPUTUNIT) ((USER_RTFLUX%FREG(I,J,K,G),I=1,USER_RTFLUX%NINTI),J=JL,JU)
         END DO
      END DO
   END DO
END IF

#ifdef DIF3D_Debug
   WRITE(Output_Unit,*)'BLOCKWIDTH = ',USER_RTFLUX%NBLOK
   DO G = 1,USER_RTFLUX%NGROUP
      DO K = 1,USER_RTFLUX%NINTK
         WRITE(Output_Unit,'("[RTFLUX]",26("."),"REGULAR FLUX FOR AXIAL PLANE ",I3," GROUP ",26("."),I16)') K,G
         WRITE(Output_Unit,'(("[RTFLUX]...J ->",3X, 5000(5X,I4,5X)))') (J,J=1,USER_RTFLUX%NINTJ)
         DO I = 1,USER_RTFLUX%NINTI
            WRITE(Output_Unit,'(("[RTFLUX]...I=",I3,2X,5000(1PE13.5,1X)))') I,(USER_RTFLUX%FREG(I,J,K,G),J=1,USER_RTFLUX%NINTJ)
         END DO
      END DO
   END DO
#endif

! Reset the blocking variable to make life easier
USER_RTFLUX%NBLOK = 1

CLOSE(UNIT=INPUTUNIT)
USER_RTFLUX%DEFINED = .TRUE.

CALL NE_Kind_FREELOGICALUNIT(INPUTUNIT)

END SUBROUTINE RTFLUX_IMPORT_BINARY

