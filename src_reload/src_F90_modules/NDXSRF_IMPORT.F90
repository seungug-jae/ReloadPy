!---------------------------------------------------------------------------------------------------------------------------------
!  NDXSRF_IMPORT(USER_NDXSRF,USER_FILENAME)
!  Imports the NDXSRF data file
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE NDXSRF_IMPORT(Output_Unit,USER_NDXSRF,USER_FILENAME)
USE NDXSRF_io
USE NE_Kind
#include "DIF3D_Types.h"
IMPLICIT NONE
! Passed in
DIF3D_Int Output_Unit
TYPE (NDXSRF_DATA) USER_NDXSRF  ! A user data variable to be defined by reading in a NDXSRF file
CHARACTER(*) USER_FILENAME
! Local
DIF3D_Int INPUTUNIT
DIF3D_Int IOS,I,J,K,L,M,N

100 FORMAT('[NDXSRF]...SORRY, BUT I MUST STOP')
101 FORMAT('[NDXSRF]',107('.'))
102 FORMAT('')
105 FORMAT('[NDXSRF]...THERE WAS A FATAL ERROR THAT OCCURED IN (IMPORT)',56('.'))

IF (USER_NDXSRF%DEFINED) CALL NDXSRF_VOID(USER_NDXSRF)   ! If the variable is already defined, void it

USER_NDXSRF%FILENAME = ADJUSTL(USER_FILENAME)
INPUTUNIT = NE_Kind_GetFreeLogicalUnit()

OPEN(UNIT=INPUTUNIT,IOSTAT=IOS,FILE=USER_FILENAME,STATUS='OLD',ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
IF (IOS .NE. 0) THEN
   WRITE(Output_Unit,105)
   WRITE(Output_Unit,'("[NDXSRF]...CANNOT OPEN FILE <",A80,">",5("."))') USER_FILENAME
   WRITE(Output_Unit,100)
   CALL Abort
END IF

#ifdef DIF3D_Debug
   WRITE(Output_Unit,101)
   WRITE(Output_Unit,'("[DLAYXS]...DEBUG PRINT OF NDXSRF IMPORT ROUTINE",52("."),A16)') USER_FILENAME
   WRITE(Output_Unit,101)
#endif
! Read Card Type 0
READ(INPUTUNIT) USER_NDXSRF%HNAME,(USER_NDXSRF%HUSE(I),I=1,2),USER_NDXSRF%IVERS
! Read Card Type 1
READ(INPUTUNIT) USER_NDXSRF%NON,USER_NDXSRF%NSN,USER_NDXSRF%NNS,USER_NDXSRF%NAN,USER_NDXSRF%NZONE,USER_NDXSRF%NSZ

#ifdef DIF3D_Debug
   WRITE(Output_Unit,'("[NDXSRF]...HNAME...................................",48("."),A16)') USER_NDXSRF%HNAME
   WRITE(Output_Unit,'("[NDXSRF]...HUSE(1).................................",48("."),A16)') USER_NDXSRF%HUSE(1)
   WRITE(Output_Unit,'("[NDXSRF]...HUSE(2).................................",48("."),A16)') USER_NDXSRF%HUSE(2)
   WRITE(Output_Unit,'("[NDXSRF]...IVERS...................................",48("."),I16)') USER_NDXSRF%IVERS
   WRITE(Output_Unit,'("[NDXSRF]...NUMBER OF NUCLIDES IN CROSS SECTION DATA",48("."),I16)') USER_NDXSRF%NON
   WRITE(Output_Unit,'("[NDXSRF]...NUMBER OF NUCLIDE SETS IDENTIFIED.......",48("."),I16)') USER_NDXSRF%NSN
   WRITE(Output_Unit,'("[NDXSRF]...MAXIMUM NUMBER OF NUCLIDES IN ANY SET...",48("."),I16)') USER_NDXSRF%NNS
   WRITE(Output_Unit,'("[NDXSRF]...NUMBER OF DIFFERENT NUCLIDES IN DATA....",48("."),I16)') USER_NDXSRF%NAN
   WRITE(Output_Unit,'("[NDXSRF]...NUMBER OF ZONES.........................",48("."),I16)') USER_NDXSRF%NZONE
   WRITE(Output_Unit,'("[NDXSRF]...NUMBER OF SUBZONES (SUBASSEMBLIES)......",48("."),I16)') USER_NDXSRF%NSZ
#endif

CALL NDXSRF_DEFINE(USER_NDXSRF,USER_NDXSRF%NON,USER_NDXSRF%NSN,USER_NDXSRF%NNS, &
                          USER_NDXSRF%NAN,USER_NDXSRF%NZONE,USER_NDXSRF%NSZ            )

! Read Card Type 2
READ(INPUTUNIT) (USER_NDXSRF%HNNAME(N),N=1,USER_NDXSRF%NON),(USER_NDXSRF%HANAME(N),N=1,USER_NDXSRF%NON),       &
                (USER_NDXSRF%WPF(N),N=1,USER_NDXSRF%NON),(USER_NDXSRF%ATWT(J),J=1,USER_NDXSRF%NAN),            &
                (USER_NDXSRF%NCLN(N),N=1,USER_NDXSRF%NON),((USER_NDXSRF%NDXS(K,L),K=1,4),L=1,USER_NDXSRF%NSN), &
                ((USER_NDXSRF%NOS(I,L),I=1,USER_NDXSRF%NNS),L=1,USER_NDXSRF%NSN),                              &
                ((USER_NDXSRF%NOR(N,L),N=1,USER_NDXSRF%NON),L=1,USER_NDXSRF%NSN)

#ifdef DIF3D_Debug
   DO I = 1,USER_NDXSRF%NON
      WRITE(Output_Unit,300)I,USER_NDXSRF%HNNAME(I),USER_NDXSRF%HANAME(I),USER_NDXSRF%WPF(I),USER_NDXSRF%NCLN(I)
      300 FORMAT('[NDXSRF]...NUCLIDE [',I5,',',A8,',',A8,'] RESERVED [',1PE13.6,'] CLASSIFICATION [',I2,']',26('.'))
   END DO 
   DO I = 1,USER_NDXSRF%NAN
      WRITE(Output_Unit,'("[NDXSRF]...ATOM WEIGHT OF UNIQUE ISOTOPE ",I4,54("."),F16.9)') I,USER_NDXSRF%ATWT(I)
   END DO 
   DO J = 1,USER_NDXSRF%NSN
      WRITE(Output_Unit,'("[NDXSRF]...THERE ARE ",I5," NUCLIDES (NDXS) IN SET",50("."),I16)') USER_NDXSRF%NDXS(1,J),J
      WRITE(Output_Unit,'("[NDXSRF]...NOS[I,#] ORDER # OF NUCLIDE I IN CROSS SECTION DATA (HNNAME) FOR THIS SET",34("."))')
      WRITE(Output_Unit,'(("[NDXSRF]...",8("[",I4,",",I4,"]",1X),8(".") ))') ((I,USER_NDXSRF%NOS(I,J)),I=1,USER_NDXSRF%NNS)
      WRITE(Output_Unit,'("[NDXSRF]...NOR[I,#] ORDER # IN THIS SET OF NUCLIDE I FROM CROSS SECTION DATA (HNNAME)",33("."))')
      WRITE(Output_Unit,'(("[NDXSRF]...",8("[",I4,",",I4,"]",1X),8(".") ))') ((I,USER_NDXSRF%NOR(I,J)),I=1,USER_NDXSRF%NON)
   END DO
#endif

! Read Card Type 3
READ(INPUTUNIT) (USER_NDXSRF%VOLZ(N),N=1,USER_NDXSRF%NZONE),(USER_NDXSRF%VFPA(N),N=1,USER_NDXSRF%NZONE), &
                (USER_NDXSRF%VLSA(M),M=1,USER_NDXSRF%NSZ),  (USER_NDXSRF%NSPA(N),N=1,USER_NDXSRF%NZONE), &
                (USER_NDXSRF%NSSA(M),M=1,USER_NDXSRF%NSZ),(USER_NDXSRF%NZSZ(M),M=1,USER_NDXSRF%NSZ)

#ifdef DIF3D_Debug
   DO I = 1,USER_NDXSRF%NZONE
      WRITE(Output_Unit,350)I,USER_NDXSRF%VOLZ(I),USER_NDXSRF%VFPA(I),USER_NDXSRF%NSPA(I)
      350 FORMAT('[NDXSRF]...ZONE ',I5,' OF VOLUME [',1PE13.6,',',1PE13.6,']',' HAS A PRIMARY ZONE ASSIGNMENT OF ',4('.'),I16)
   END DO 
   DO I = 1,USER_NDXSRF%NSZ
      WRITE(Output_Unit,400)I,USER_NDXSRF%VLSA(I),USER_NDXSRF%NSSA(I),USER_NDXSRF%NZSZ(I)
      400 FORMAT('[NDXSRF]...SUBZONE ',I5,' OF VOLUME ',1PE13.6,' IS IN SET ',I5,' AND IS PART OF ZONE',15('.'),I16)
   END DO
#endif

CLOSE(UNIT=INPUTUNIT) 
USER_NDXSRF%DEFINED = .TRUE.

CALL NE_Kind_FREELOGICALUNIT(INPUTUNIT)

END SUBROUTINE NDXSRF_IMPORT

