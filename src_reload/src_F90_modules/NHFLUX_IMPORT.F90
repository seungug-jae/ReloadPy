SUBROUTINE NHFLUX_IMPORT(Output_Unit,USER_NHFLUX,USER_FILENAME,FileContainsForward)
USE NHFLUX_io
USE NE_Kind
#include "DIF3D_Types.h"
!#define DIF3D_Debug
IMPLICIT NONE
! Passed in
DIF3D_Int Output_Unit
TYPE (NHFLUX_DATA) USER_NHFLUX  ! A user data variable to be defined by reading in a NHFLUX file
CHARACTER(*) USER_FILENAME
DIF3D_Log FileContainsForward
! Local
DIF3D_Int File_Unit
DIF3D_Int IOS,I,J,K,L,M,N,III,GG

100 FORMAT('[NHFLUX]...SORRY, BUT I MUST STOP')
101 FORMAT('[NHFLUX]',107('.'))
102 FORMAT('')
105 FORMAT('[NHFLUX]...THERE WAS A FATAL ERROR THAT OCCURED IN (IMPORT)',56('.'))

IF (USER_NHFLUX%DEFINED) CALL NHFLUX_VOID(USER_NHFLUX)   ! If the variable is already defined, void it

USER_NHFLUX%FILENAME = ADJUSTL(USER_FILENAME)
File_Unit = NE_Kind_GetFreeLogicalUnit()

OPEN(UNIT=File_Unit,IOSTAT=IOS,FILE=USER_FILENAME,STATUS='OLD',ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
IF (IOS .NE. 0) THEN
   WRITE(Output_Unit,105)
   WRITE(Output_Unit,'("[NHFLUX]...CANNOT OPEN FILE <",A80,">",5("."))') USER_FILENAME
   WRITE(Output_Unit,100)
   CALL Abort
END IF

#ifdef DIF3D_Debug
   WRITE(Output_Unit,101)
   WRITE(Output_Unit,'("[DLAYXS]...DEBUG PRINT OF NHFLUX IMPORT ROUTINE",52("."),A16)') USER_FILENAME
   WRITE(Output_Unit,101)
#endif


! Read Card Type 0
READ(File_Unit) USER_NHFLUX%HNAME,(USER_NHFLUX%HUSE(I),I=1,2),USER_NHFLUX%IVERS

! Read Card Type 1
READ(File_Unit) &
   USER_NHFLUX%NDIM,  USER_NHFLUX%NGROUP, USER_NHFLUX%NINTI,  USER_NHFLUX%NINTJ,  USER_NHFLUX%NINTK,  USER_NHFLUX%ITER, &
   USER_NHFLUX%EFFK,  USER_NHFLUX%POWER,  USER_NHFLUX%NSURF,  USER_NHFLUX%NMOM,   USER_NHFLUX%NINTXY, USER_NHFLUX%NPCXY, &
   USER_NHFLUX%NSCOEF,USER_NHFLUX%ITRORD, USER_NHFLUX%IAPRX,  USER_NHFLUX%ILEAK,  USER_NHFLUX%IAPRXZ, USER_NHFLUX%ILEAKZ, &
   USER_NHFLUX%IORDER,USER_NHFLUX%NPCBDY, USER_NHFLUX%NPCSYM, USER_NHFLUX%NPCSEC, USER_NHFLUX%IWNHFL, USER_NHFLUX%NMOMS, &
   USER_NHFLUX%DUM1, USER_NHFLUX%DUM2, USER_NHFLUX%DUM3, USER_NHFLUX%DUM4, USER_NHFLUX%DUM5, USER_NHFLUX%DUM6

IF (USER_NHFLUX%IVERS .EQ. 1) USER_NHFLUX%NPCBDY = USER_NHFLUX%NPCXY - USER_NHFLUX%NSURF*USER_NHFLUX%NINTXY

#ifdef DIF3D_Debug
   WRITE(Output_Unit,'("[NHFLUX]...HNAME.................................................",48("."),A16)') USER_NHFLUX%HNAME
   WRITE(Output_Unit,'("[NHFLUX]...HUSE(1)...............................................",48("."),A16)') USER_NHFLUX%HUSE(1)
   WRITE(Output_Unit,'("[NHFLUX]...HUSE(2)...............................................",48("."),A16)') USER_NHFLUX%HUSE(2)
   WRITE(Output_Unit,'("[NHFLUX]...IVERS.................................................",48("."),I16)') USER_NHFLUX%IVERS
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF DIMENSIONS..................................",48("."),I16)') USER_NHFLUX%NDIM
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF ENERGY GROUPS...............................",48("."),I16)') USER_NHFLUX%NGROUP
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF 1st DIM FINE MESH INTERVALS.................",48("."),I16)') USER_NHFLUX%NINTI
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF 2nd DIM FINE MESH INTERVALS.................",48("."),I16)') USER_NHFLUX%NINTJ
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF 3rd DIM FINE MESH INTERVALS.................",48("."),I16)') USER_NHFLUX%NINTK
   WRITE(Output_Unit,'("[NHFLUX]...OUTER ITER NUMBER AT WHICH FLUX WAS WRITTEN...........",48("."),I16)') USER_NHFLUX%ITER
   WRITE(Output_Unit,'("[NHFLUX]...EFFECTIVE MULTIPLICATION FACTOR.......................",48("."),E16.9)') USER_NHFLUX%EFFK
   WRITE(Output_Unit,'("[NHFLUX]...POWER IN WATTS TO WHICH FLUX IS NORMALIZED............",48("."),E16.9)') USER_NHFLUX%POWER
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF XY-PLANE SURFACES PER NODE..................",48("."),I16)') USER_NHFLUX%NSURF
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF FLUX MOMENTS IN NODAL APPROXIMATION.........",48("."),I16)') USER_NHFLUX%NMOM
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF MESH CELLS (NODES) ON XY-PLANE..............",48("."),I16)') USER_NHFLUX%NINTXY
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF XY-DIRECTED PARTIAL CURRENTS ON XY-PLANE....",48("."),I16)') USER_NHFLUX%NPCXY
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF PARTIAL CURRENT MOMENTS PER NODE SURFACE....",48("."),I16)') USER_NHFLUX%NSCOEF
   WRITE(Output_Unit,'("[NHFLUX]...ORDER OF THE POLYNOMIAL APPROXIMATION OF THE SOURCE...",48("."),I16)') USER_NHFLUX%ITRORD
   WRITE(Output_Unit,'("[NHFLUX]...ORDER OF THE POLYNOMIAL APPROXIMATION OF THE FLUXES...",48("."),I16)') USER_NHFLUX%IAPRX
   WRITE(Output_Unit,'("[NHFLUX]...ORDER OF THE POLYNOMIAL APPROXIMATION OF THE LEAKAGES.",48("."),I16)') USER_NHFLUX%ILEAK
   WRITE(Output_Unit,'("[NHFLUX]...ORDER OF THE PN EXPANSION OF THE FLUX.................",48("."),I16)') USER_NHFLUX%IAPRXZ
   WRITE(Output_Unit,'("[NHFLUX]...ORDER OF THE PN EXPANSION OF THE LEAKAGE..............",48("."),I16)') USER_NHFLUX%ILEAKZ
   WRITE(Output_Unit,'("[NHFLUX]...IORDER - DIF3D VERSION................................",48("."),I16)') USER_NHFLUX%IORDER
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON OUTER XY-PLANE BOUN.....",48("."),I16)') USER_NHFLUX%NPCBDY
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON SYMMETRIC XY-PLANE BOUN.",48("."),I16)') USER_NHFLUX%NPCSYM
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF OUTGOING PAR CUR ON SECTOR XY-PLANE BOUN....",48("."),I16)') USER_NHFLUX%NPCSEC
   WRITE(Output_Unit,'("[NHFLUX]...NHFLUX CONTENT........................................",48("."),I16)') USER_NHFLUX%IWNHFL
   WRITE(Output_Unit,'("[NHFLUX]...NUMBER OF ODD PARITY FLUX MOMENTS IN NODAL APPROX.....",48("."),I16)') USER_NHFLUX%NMOMS
   WRITE(Output_Unit,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM1
   WRITE(Output_Unit,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM2
   WRITE(Output_Unit,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM3
   WRITE(Output_Unit,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM4
   WRITE(Output_Unit,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM5
   WRITE(Output_Unit,'("[NHFLUX]...RESERVED FOR FUTURE USE...............................",48("."),I16)') USER_NHFLUX%DUM6
#endif

CALL NHFLUX_DEFINE(USER_NHFLUX,USER_NHFLUX%NSURF,USER_NHFLUX%NINTXY,USER_NHFLUX%NPCBDY,USER_NHFLUX%NPCSYM,USER_NHFLUX%NPCSEC,  &
  USER_NHFLUX%NMOM,USER_NHFLUX%NINTK,USER_NHFLUX%NSCOEF,USER_NHFLUX%NPCXY,USER_NHFLUX%NGROUP,USER_NHFLUX%NMOMS,USER_NHFLUX%IWNHFL)

! Read Card Type 2
IF(USER_NHFLUX%NSURF .GT. 1) THEN

   READ(File_Unit)   ((USER_NHFLUX%IPCPNT(K,L),K=1,USER_NHFLUX%NSURF),L=1,USER_NHFLUX%NINTXY), &
                      (USER_NHFLUX%IPCBDY(N),N=1,USER_NHFLUX%NPCBDY), &
                      (USER_NHFLUX%ITRMAP(N),N=1,USER_NHFLUX%NINTXY), &
                      (USER_NHFLUX%IPCSYM(N),N=1,USER_NHFLUX%NPCSYM + USER_NHFLUX%NPCSEC), &
                      (USER_NHFLUX%IPCSCP(N),N=1,USER_NHFLUX%NPCSYM + USER_NHFLUX%NPCSEC)

#ifdef DIF3D_Debug
   DO J = 1,USER_NHFLUX%NINTXY
      WRITE(Output_Unit,'(("[NHFLUX]...POINTERS TO INCOMING XY PLANE PARTIAL CURRENTS...",8("[",I4,",",I16,"]",1X) ))') &
         ((I,USER_NHFLUX%IPCPNT(I,J)),I=1,USER_NHFLUX%NSURF)
   END DO
   DO I = 1,USER_NHFLUX%NPCBDY
      WRITE(Output_Unit,'("[NHFLUX]...POINTERS TO OUTGOING PARTIAL CURRENTS.................",I4,54("."),I16)') &
         I,USER_NHFLUX%IPCBDY(I)
   END DO
   DO I = 1,USER_NHFLUX%NINTXY
      WRITE(Output_Unit,'("[NHFLUX]...TRANSFORMATION MAP BETWEEN NODAL AND GEODST...........",I4,54("."),I16)') &
         I,USER_NHFLUX%ITRMAP(I)
   END DO
   DO I = 1,USER_NHFLUX%NPCSYM + USER_NHFLUX%NPCSEC
      WRITE(Output_Unit,'("[NHFLUX]...POINTERS TO OUTGOING PAR CURR ON SYM BOUND............",I4,54("."),I16)') &
         I,USER_NHFLUX%IPCSYM(I)
   END DO
   DO I = 1,USER_NHFLUX%NPCSYM + USER_NHFLUX%NPCSEC
      WRITE(Output_Unit,'("[NHFLUX]...POINTERS TO INGOING PAR CURR ON SYM AND SEC BOUND.....",I4,54("."),I16)') &
         I,USER_NHFLUX%IPCSCP(I)
   END DO
#endif

ENDIF

USER_NHFLUX%ContainsForwardData = FileContainsForward

DO GG=1,USER_NHFLUX%NGROUP
   IF (FileContainsForward) THEN
      N = GG
   ELSE
      N = USER_NHFLUX%NGROUP+1-GG
   END IF
   ! Read Card Type 3
   IF ( (USER_NHFLUX%IWNHFL .EQ. 0) .OR. (USER_NHFLUX%IWNHFL .EQ. 1)) THEN
         III = USER_NHFLUX%NMOM
         DO J=1,USER_NHFLUX%NINTK
            READ(File_Unit)   ((USER_NHFLUX%FLUX(K,L,J,N),K=1,USER_NHFLUX%NMOM),L=1,USER_NHFLUX%NINTXY),  &
                              ((USER_NHFLUX%FLUX(III+K,L,J,N),K=1,USER_NHFLUX%NMOMS),L=1,USER_NHFLUX%NINTXY)
         ENDDO
#ifdef DIF3D_Debug
         III = USER_NHFLUX%NMOM+USER_NHFLUX%NMOMS
         DO K = 1,USER_NHFLUX%NINTK
            WRITE(Output_Unit,'("[NHFLUX]...REGULAR FLUX MOMENTS FOR AXIAL PLANE.",I6)') K
            WRITE(Output_Unit,'("[NHFLUX]...XY Node; Nodal moments -> ")')
            DO J = 1,USER_NHFLUX%NINTXY
               WRITE(Output_Unit,'(I5,1X,5000(1PE16.9,1X))') J,(USER_NHFLUX%FLUX(I,J,K,N),I=1,III)
            END DO
         END DO
#endif
   ENDIF


   ! Read Card Type 4
   IF ( (USER_NHFLUX%IWNHFL .EQ. 0) .OR. (USER_NHFLUX%IWNHFL .EQ. 2)) THEN
      DO J=1,USER_NHFLUX%NINTK
         READ(File_Unit)   ((USER_NHFLUX%PCURRH(K,L,J,N),K=1,USER_NHFLUX%NSCOEF),L=1,USER_NHFLUX%NPCXY)
      ENDDO
#ifdef DIF3D_Debug
      DO K = 1,USER_NHFLUX%NINTK
         WRITE(Output_Unit,'("[NHFLUX]...RADIALLY DIRECTED PARTIAL CURRENTS FOR AXIAL PLANE.",I6)') K
         WRITE(Output_Unit,'("[NHFLUX]...XY Surface Index; Current moments -> ")')
         DO J = 1,USER_NHFLUX%NPCXY
            WRITE(Output_Unit,'(I5,1X,5000(1PE16.9,1X))') J,(USER_NHFLUX%PCURRH(I,J,K,N),I=1,USER_NHFLUX%NSCOEF)
         END DO
      END DO
#endif
   ENDIF

   ! Read Card Type 5
   IF ( (USER_NHFLUX%NDIM .EQ. 3) .AND. ((USER_NHFLUX%IWNHFL .EQ. 0) .OR. (USER_NHFLUX%IWNHFL .EQ. 2))) THEN
      DO J=1,USER_NHFLUX%NINTK+1
         READ(File_Unit)   (((USER_NHFLUX%PCURRZ(K,L,M,J,N),K=1,USER_NHFLUX%NSCOEF),L=1,USER_NHFLUX%NINTXY),M=1,2)
      ENDDO 
#ifdef DIF3D_Debug
      DO J = 1,USER_NHFLUX%NINTK+1
         DO M = 1,2
            IF (M .EQ. 1) THEN
               WRITE(Output_Unit,'("[NHFLUX]...Z-DIRECTED PARTIAL CURRENTS FOR LOWER SURFACE AXIAL PLANE ",I6)') J
            ELSE
               WRITE(Output_Unit,'("[NHFLUX]...Z-DIRECTED PARTIAL CURRENTS FOR UPPER SURFACE AXIAL PLANE.",I6)') J
            END IF
            DO L=1,USER_NHFLUX%NINTXY
               WRITE(Output_Unit,'(I5,1X,5000(1PE16.9,1X))') L,(USER_NHFLUX%PCURRZ(K,L,M,J,N),K=1,USER_NHFLUX%NSCOEF)
            ENDDO
         END DO
      END DO
#endif
   ENDIF

ENDDO


CLOSE(UNIT=File_Unit)
USER_NHFLUX%DEFINED = .TRUE.

CALL NE_Kind_FREELOGICALUNIT(File_Unit)

END SUBROUTINE NHFLUX_IMPORT

