!---------------------------------------------------------------------------------------------------------------------------------
!  GEODST_IMPORT_BINARY(USER_GEODST,USER_FILENAME)
!  Reads in the binary GEODST data file
!---------------------------------------------------------------------------------------------------------------------------------
SUBROUTINE GEODST_IMPORT_BINARY(Output_Unit,USER_GEODST,USER_FILENAME)
USE GEODST_io
USE NE_Kind
#include "DIF3D_Types.h"
IMPLICIT NONE
! Passed in
DIF3D_Int Output_Unit
TYPE (GEODST_DATA) USER_GEODST                  ! A user GEODST data structure
CHARACTER(*) USER_FILENAME                         ! The name of the text input file to load
! Local junk
DIF3D_Int FileUnit
DIF3D_Int IOS,I,J,K,N
DIF3D_Int T_IGOM,T_NZONE,T_NREG,T_NCINTI,T_NCINTJ,T_NCINTK,T_NINTI,T_NINTJ,T_NINTK,T_NBS,T_NBCS,T_NIBCS,T_NZWBB,T_NRASS

100 FORMAT('[GEODST]...SORRY, BUT I MUST STOP')
101 FORMAT('[GEODST]',107('.'))
102 FORMAT('')
105 FORMAT('[GEODST]...THERE WAS A FATAL ERROR THAT OCCURED IN (IMPORT)',56('.'))

IF (USER_GEODST%DEFINED) CALL GEODST_VOID(USER_GEODST)   ! If the variable is already defined, void it

USER_GEODST%FILENAME = ADJUSTL(USER_FILENAME)
FileUnit = NE_Kind_GetFreeLogicalUnit()

! Open the input file for reading
OPEN(UNIT=FileUnit,IOSTAT=IOS,FILE=USER_FILENAME,STATUS='OLD',ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
IF (IOS .NE. 0) THEN
   WRITE(Output_Unit,105)
   WRITE(Output_Unit,'("[GEODST]...CANNOT OPEN FILE <",A80,">",5("."))') USER_FILENAME
   WRITE(Output_Unit,100)
   CALL Abort
END IF

#ifdef DIF3D_Debug
   WRITE(Output_Unit,101)
   WRITE(Output_Unit,'("[GEODST]...DEBUG PRINT OF GEODST IMPORT ROUTINE",52("."),A16)') USER_FILENAME
   WRITE(Output_Unit,101)
#endif

! Card type 0
READ(FileUnit) USER_GEODST%HNAME,(USER_GEODST%HUSE(I),I=1,2),USER_GEODST%IVERS
! Card type 1
READ(FileUnit) USER_GEODST%IGOM,USER_GEODST%NZONE,USER_GEODST%NREG,USER_GEODST%NZCL,                                             &
                  USER_GEODST%NCINTI,USER_GEODST%NCINTJ,USER_GEODST%NCINTK,USER_GEODST%NINTI,USER_GEODST%NINTJ,USER_GEODST%NINTK, &
                  USER_GEODST%IMB1,USER_GEODST%IMB2,USER_GEODST%JMB1,USER_GEODST%JMB2,USER_GEODST%KMB1,USER_GEODST%KMB2,          &
                  USER_GEODST%NBS,USER_GEODST%NBCS,USER_GEODST%NIBCS,USER_GEODST%NZWBB,USER_GEODST%NTRIAG,USER_GEODST%NRASS,      &
                  USER_GEODST%NTHPT,(USER_GEODST%NGOP(I),I=1,4)

#ifdef DIF3D_Debug
   WRITE(Output_Unit,'("[GEODST]...HNAME...........................................",40("."),A16)') USER_GEODST%HNAME
   WRITE(Output_Unit,'("[GEODST]...HUSE(1).........................................",40("."),A16)') USER_GEODST%HUSE(1)
   WRITE(Output_Unit,'("[GEODST]...HUSE(2).........................................",40("."),A16)') USER_GEODST%HUSE(2)
   WRITE(Output_Unit,'("[GEODST]...IVERS...........................................",40("."),I16)') USER_GEODST%IVERS
   WRITE(Output_Unit,'("[GEODST]...GEOMETRY TYPE...................................",40("."),I16)') USER_GEODST%IGOM
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF ZONES.................................",40("."),I16)') USER_GEODST%NZONE
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF REGIONS...............................",40("."),I16)') USER_GEODST%NREG
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF ZONE CLASSIFICATIONS..................",40("."),I16)') USER_GEODST%NZCL
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF FIRST  DIMENSION COARSE MESH INTERVALS",40("."),I16)') USER_GEODST%NCINTI
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF SECOND DIMENSION COARSE MESH INTERVALS",40("."),I16)') USER_GEODST%NCINTJ
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF THIRD  DIMENSION COARSE MESH INTERVALS",40("."),I16)') USER_GEODST%NCINTK
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF FIRST  DIMENSION FINE   MESH INTERVALS",40("."),I16)') USER_GEODST%NINTI
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF SECOND DIMENSION FINE   MESH INTERVALS",40("."),I16)') USER_GEODST%NINTJ
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF THIRD  DIMENSION FINE   MESH INTERVALS",40("."),I16)') USER_GEODST%NINTK
   WRITE(Output_Unit,'("[GEODST]...FIRST  BOUNDARY CONDITION ON FIRST  DIMENSION...",40("."),I16)') USER_GEODST%IMB1
   WRITE(Output_Unit,'("[GEODST]...SECOND BOUNDARY CONDITION ON FIRST  DIMENSION...",40("."),I16)') USER_GEODST%IMB2
   WRITE(Output_Unit,'("[GEODST]...FIRST  BOUNDARY CONDITION ON SECOND DIMENSION...",40("."),I16)') USER_GEODST%JMB1
   WRITE(Output_Unit,'("[GEODST]...SECOND BOUNDARY CONDITION ON SECOND DIMENSION...",40("."),I16)') USER_GEODST%JMB2
   WRITE(Output_Unit,'("[GEODST]...FIRST  BOUNDARY CONDITION ON THIRD  DIMENSION...",40("."),I16)') USER_GEODST%KMB1
   WRITE(Output_Unit,'("[GEODST]...SECOND BOUNDARY CONDITION ON THIRD  DIMENSION...",40("."),I16)') USER_GEODST%KMB2
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF BUCKLING SPECIFICATIONS...............",40("."),I16)') USER_GEODST%NBS
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF CONSTANTS FOR EXTERNAL BOUNDARIES.....",40("."),I16)') USER_GEODST%NBCS
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF CONSTANTS FOR INTERNAL BOUNDARIES.....",40("."),I16)') USER_GEODST%NIBCS
   WRITE(Output_Unit,'("[GEODST]...NUMBER OF ZONES WHICH ARE BLACK ABSORBERS.......",40("."),I16)') USER_GEODST%NZWBB
   WRITE(Output_Unit,'("[GEODST]...TRIANGULAR/HEXAGONAL GEOMETRY OPTION............",40("."),I16)') USER_GEODST%NTRIAG
   WRITE(Output_Unit,'("[GEODST]...REGION ASSIGNMENT...............................",40("."),I16)') USER_GEODST%NRASS
   WRITE(Output_Unit,'("[GEODST]...ORIENTATION OF FIRST FINE MESH INTERVAL (TRI)...",40("."),I16)') USER_GEODST%NTHPT
   WRITE(Output_Unit,'("[GEODST]...RESERVED 1......................................",40("."),I16)') USER_GEODST%NGOP(1)
   WRITE(Output_Unit,'("[GEODST]...RESERVED 2......................................",40("."),I16)') USER_GEODST%NGOP(2)
   WRITE(Output_Unit,'("[GEODST]...RESERVED 3......................................",40("."),I16)') USER_GEODST%NGOP(3)
   WRITE(Output_Unit,'("[GEODST]...RESERVED 4......................................",40("."),I16)') USER_GEODST%NGOP(4)
#endif

T_IGOM     = USER_GEODST%IGOM  
T_NZONE    = USER_GEODST%NZONE 
T_NREG     = USER_GEODST%NREG  
T_NCINTI   = USER_GEODST%NCINTI
T_NCINTJ   = USER_GEODST%NCINTJ
T_NCINTK   = USER_GEODST%NCINTK
T_NINTI    = USER_GEODST%NINTI 
T_NINTJ    = USER_GEODST%NINTJ 
T_NINTK    = USER_GEODST%NINTK 
T_NBS      = USER_GEODST%NBS   
T_NBCS     = USER_GEODST%NBCS  
T_NIBCS    = USER_GEODST%NIBCS 
T_NZWBB    = USER_GEODST%NZWBB 
T_NRASS    = USER_GEODST%NRASS 

CALL GEODST_DEFINE(USER_GEODST,T_IGOM,T_NZONE,T_NREG,T_NCINTI,T_NCINTJ,T_NCINTK,T_NINTI,T_NINTJ,T_NINTK,  &
                          T_NBS,T_NBCS,T_NIBCS,T_NZWBB,T_NRASS        )


IF      (USER_GEODST%IGOM .GT. 0 .AND. USER_GEODST%IGOM .LE. 3) THEN
   ! Card Type 2
   READ(FileUnit) (USER_GEODST%XMESH(I),I=1,USER_GEODST%NCINTI+1),(USER_GEODST%IFINTS(I),I=1,USER_GEODST%NCINTI)
ELSE IF (USER_GEODST%IGOM .GE. 6 .AND. USER_GEODST%IGOM .LE. 11) THEN
   ! Card Type 3
   READ(FileUnit) (USER_GEODST%XMESH(I),I=1,USER_GEODST%NCINTI+1),(USER_GEODST%YMESH(J),J=1,USER_GEODST%NCINTJ+1),  &
                   (USER_GEODST%IFINTS(I),I=1,USER_GEODST%NCINTI),(USER_GEODST%JFINTS(J),J=1,USER_GEODST%NCINTJ)
ELSE IF (USER_GEODST%IGOM .GE. 12) THEN
   ! Card Type 4
   READ(FileUnit) (USER_GEODST%XMESH(I),I=1,USER_GEODST%NCINTI+1),(USER_GEODST%YMESH(J),J=1,USER_GEODST%NCINTJ+1),   &
                   (USER_GEODST%ZMESH(K),K=1,USER_GEODST%NCINTK+1),(USER_GEODST%IFINTS(I),I=1,USER_GEODST%NCINTI),    &
                   (USER_GEODST%JFINTS(J),J=1,USER_GEODST%NCINTJ),(USER_GEODST%KFINTS(K),K=1,USER_GEODST%NCINTK)
END IF

#ifdef DIF3D_Debug
   WRITE(Output_Unit,'("[GEODST]...X COARSE MESH BOUNDARIES",80("."))')
   DO I = 1,USER_GEODST%NCINTI
      WRITE(Output_Unit,200) USER_GEODST%XMESH(I),USER_GEODST%IFINTS(I),USER_GEODST%XMESH(I+1)
   END DO
   200 FORMAT('[GEODST]...',F13.6,1X,I3,1X,F13.6)
   IF (USER_GEODST%NCINTJ .GT. 1) THEN
      WRITE(Output_Unit,'("[GEODST]...Y COARSE MESH BOUNDARIES",80("."))')
      DO I = 1,USER_GEODST%NCINTJ
         WRITE(Output_Unit,200) USER_GEODST%YMESH(I),USER_GEODST%JFINTS(I),USER_GEODST%YMESH(I+1)
      END DO
   END IF
   IF (USER_GEODST%NCINTK .GT. 1) THEN
      WRITE(Output_Unit,'("[GEODST]...Z COARSE MESH BOUNDARIES",80("."))')
      DO I = 1,USER_GEODST%NCINTK
         WRITE(Output_Unit,200) USER_GEODST%ZMESH(I),USER_GEODST%KFINTS(I),USER_GEODST%ZMESH(I+1)
      END DO
   END IF
#endif

IF ((USER_GEODST%IGOM.GT.0) .OR. (USER_GEODST%NBS.GT.0)) THEN
   ! Card type 5
   READ(FileUnit) (USER_GEODST%VOLR(N),N=1,USER_GEODST%NREG),(USER_GEODST%BSQ(N),N=1,USER_GEODST%NBS),     &
                   (USER_GEODST%BNDC(N),N=1,USER_GEODST%NBCS),(USER_GEODST%BNCI(N),N=1,USER_GEODST%NIBCS),  &
                   (USER_GEODST%NZHBB(N),N=1,USER_GEODST%NZWBB),(USER_GEODST%NZC(N),N=1,USER_GEODST%NZONE), &
                   (USER_GEODST%NZNR(N),N=1,USER_GEODST%NREG)
#ifdef DIF3D_Debug
      250 FORMAT('[GEODST]...',5(I4,1X,E13.6,1X))
      255 FORMAT('[GEODST]...',8(I5,1X,I5,1X))
      260 FORMAT('[GEODST]...',11(I5,1X,I2,1X))
      WRITE(Output_Unit,'("[GEODST]...REGION VOLUMES",90("."))')
      WRITE(Output_Unit,250) (N,USER_GEODST%VOLR(N),N=1,USER_GEODST%NREG)
      WRITE(Output_Unit,'("[GEODST]...BUCKLING (B^2) VALUES (CM^-2)",75("."))')
      WRITE(Output_Unit,250) (N,USER_GEODST%BSQ(N),N=1,USER_GEODST%NBS)
      WRITE(Output_Unit,'("[GEODST]...BOUNDARY CONSTANTS (DEL PHI/PHI =-C/D)",66("."))')
      WRITE(Output_Unit,250) (N,USER_GEODST%BNDC(N),N=1,USER_GEODST%NBCS)
      WRITE(Output_Unit,'("[GEODST]...INTERNAL BLACK BOUNDARY CONSTANTS",71("."))')
      WRITE(Output_Unit,250) (N,USER_GEODST%BNCI(N),N=1,USER_GEODST%NIBCS)
      WRITE(Output_Unit,'("[GEODST]...ZONE NUMBERS WITH BLACK ABSORBER CONDITIONS",61("."))')
      WRITE(Output_Unit,255) (N,USER_GEODST%NZHBB(N),N=1,USER_GEODST%NZWBB)
      WRITE(Output_Unit,'("[GEODST]...ZONE CLASSIFICATIONS",84("."))')
      WRITE(Output_Unit,260) (N,USER_GEODST%NZC(N),N=1,USER_GEODST%NZONE)
      WRITE(Output_Unit,'("[GEODST]...ZONE NUMBER ASSIGNED TO EACH REGION",69("."))')
      WRITE(Output_Unit,255) (N,USER_GEODST%NZNR(N),N=1,USER_GEODST%NREG)
#endif
END IF

IF ((USER_GEODST%IGOM.GT.0) .AND. (USER_GEODST%NRASS.EQ.0)) THEN
   DO K = 1,USER_GEODST%NCINTK
      ! Card type 6
      READ(FileUnit)((USER_GEODST%MR(I,J,K),I=1,USER_GEODST%NCINTI),J=1,USER_GEODST%NCINTJ)
   END DO
#ifdef DIF3D_Debug
      WRITE(Output_Unit,'("[GEODST]...REGION NUMBERS Z ASSIGNED TO COARSE MESH INTERVALS I,J,K: (I,J,K)=Z",37("."))')
      300 FORMAT('[GEODST]...',4('(',I3,',',I3,',',I3,')=',I5,3X))
      WRITE(Output_Unit,300) (((I,J,K,USER_GEODST%MR(I,J,K),I=1,USER_GEODST%NCINTI),J=1,USER_GEODST%NCINTJ),K=1,USER_GEODST%NCINTK)
#endif
ELSE IF ((USER_GEODST%IGOM.GT.0) .AND. (USER_GEODST%NRASS.EQ.1)) THEN
   DO K = 1,USER_GEODST%NINTK
      ! Card type 7
      READ(FileUnit)((USER_GEODST%MR(I,J,K),I=1,USER_GEODST%NINTI),J=1,USER_GEODST%NINTJ)
   END DO
#ifdef DIF3D_Debug
      WRITE(Output_Unit,'("[GEODST]...REGION NUMBERS Z ASSIGNED TO FINE MESH INTERVALS I,J,K: (I,J,K)=Z",39("."))')
      WRITE(Output_Unit,300) (((I,J,K,USER_GEODST%MR(I,J,K),I=1,USER_GEODST%NINTI),J=1,USER_GEODST%NINTJ),K=1,USER_GEODST%NINTK)
#endif
END IF

CLOSE(UNIT=FileUnit)

CALL NE_Kind_FREELOGICALUNIT(FileUnit)

END SUBROUTINE GEODST_IMPORT_BINARY
